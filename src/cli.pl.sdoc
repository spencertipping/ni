CLI grammar.
ni's command line grammar uses some patterns on top of the parser combinator
primitives defined in parse.pl.sdoc. Probably the most important one to know
about is the long/short option dispatch, which looks like this:

| option = alt @longs, dsp %shorts

our %contexts;
our %long_names;
our %long_refs;
our %short_refs;

sub defcontext($) {
  my ($c) = @_;
  $short_refs{$c} = {};
  $long_refs{$c}  = [pdspr %{$short_refs{$c}}];
  $long_names{$c} = ['<short dispatch>'];
  $contexts{$c}   = paltr @{$long_refs{$c}};

  defparseralias "$c/short",  pdspr %{$short_refs{$c}};
  defparseralias "$c/op",     $contexts{$c};
  defparseralias "$c/suffix", prep $contexts{$c};
  defparseralias "$c/series", prep pn 1, popt pempty, $contexts{$c}, popt pempty;
  defparseralias "$c/lambda", pn 1, prc qr/\[/, parser "$c/series", prx qr/\]/;
  defparseralias "$c/qfn",    palt parser "$c/lambda", parser "$c/suffix";

  defparseralias "$c/cli",       pn 0, parser "$c/series", pend;
  defparseralias "$c/cli_debug", parser "$c/series";
}

sub defshort($$) {
  my ($context, $dsp) = split /\//, $_[0], 2;
  die "ni: defshort cannot be used to redefine '$_[0]' (use rmshort first)"
    if exists $short_refs{$context}{$dsp};
  defparseralias "$context/short/$dsp" => $_[1];
  $short_refs{$context}{$dsp} = ["$context/short/$dsp"];
}

sub deflong($$) {
  my ($context, $name) = split /\//, $_[0], 2;
  defparseralias "$context/long/$name" => $_[1];
  unshift @{$long_names{$context}}, $name;
  unshift @{$long_refs{$context}}, ["$context/long/$name"];
}

sub rmshort($) {
  my ($context, $dsp) = split /\//, $_[0], 2;
  delete $short_refs{$context}{$dsp};
  delete $ni::parsers{"$context/short/$dsp"};
}

sub cli(@) {my ($r) = parse parser '/cli', @_; $r}

Extensible parse elements.
These patterns come up a lot, and it's worth being able to autogenerate their
documentation.

sub defalt($$@) {
  no strict 'refs';
  my ($name, $doc, @entries) = @_;
  my $vname = __PACKAGE__ . "::$name";
  @{$vname} = @entries;
  *{__PACKAGE__ . "::def$name"} = sub ($) {unshift @{$vname}, $_[0]};
  paltr @{$vname};
}

sub defdsp($$%) {
  no strict 'refs';
  my ($name, $doc, %entries) = @_;
  my $vname = __PACKAGE__ . "::$name";
  %{$vname} = %entries;
  *{__PACKAGE__ . "::def$name"} = sub ($$) {${$vname}{$_[0]} = $_[1]};
  pdspr %{$vname};
}
