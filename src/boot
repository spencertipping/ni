#!/usr/bin/env perl
chomp($ni::license = <<'_');
ni: https://github.com/spencertipping/ni
Copyright (c) 2016-2017 Spencer Tipping

MIT license

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
_
eval($ni::context{'ni.module:/boot'} = <<'_');
sub ni::context {
  my @r = @ni::context{@_};
  return wantarray ? @r : $r[0] unless grep !defined, @r;
  die 'ni::context: failed to resolve '
    . join(', ', grep !defined($ni::context{$_}), @_);
}

package ni::constructable;
sub new {
  my $class = shift;
  my $self = bless {@_}, $class;
  $self->init;
  $self;
}

package ni::named;
use overload qw/"" name/;
sub scheme {(my $s = ref shift) =~ s/::/./g; $s}
sub name {my $self = shift; $self->scheme . ":$$self{name}"}

package ni::transient_identity;
push our @ISA, 'ni::named';
use Scalar::Util qw/weaken/;
sub DESTROY {delete $ni::context{shift->name}}
sub add_to_context {my $self = shift; weaken($ni::context{$self->name} = $self); $self}

package ni::persistent_identity;
push our @ISA, 'ni::named';
sub add_to_context {my $self = shift; $ni::context{$self->name} = $self}

package ni::boot_module;
push our @ISA, qw/ni::constructable ni::persistent_identity/;
sub scheme {'ni.module'}
sub init {
  my $self = shift;
  chomp $$self{code};
  $self->add_to_context;
}
ni::boot_module->new(name => '/boot', code => ni::context 'ni.module:/boot');

package ni::module;
push our @ISA, qw/ni::constructable ni::persistent_identity/;
sub init {my $self = shift; $self->add_to_context; $self->eval}
sub eval {my $self = shift; eval $$self{code}; die "$@ evaluating $$self{name}" if $@}
_
die "$@ evaluating ni.module:/boot" if $@;
