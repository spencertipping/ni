Streaming data sources.
Common ways to read data, most notably from files and directories. Also
included are numeric generators, shell commands, etc.

use POSIX ();

our $pager_fh;

sub child_status_ok($) {$_[0] == 0 or ($_[0] & 127) == 13}

$ni::main_operator = sub {
  my @children;

  if (-t STDIN) {
    nuke_stdin;
  } else {
    # Fix for bugs/2016.0918.replicated-garbage.md: forcibly flush the STDIN
    # buffer so no child process gets bogus data.
    cdup2 0, 3;
    close STDIN;
    cdup2 3, 0;
    POSIX::close 3;
    open STDIN, '<&=0' or die "ni: failed to reopen STDIN: $!";

    push @children, sicons {sdecode};
  }

  @$_ and push @children, sicons {operate @$_} for @_;

  if (-t STDOUT) {
    $pager_fh = siproc {exec 'less' or exec 'more' or sio};
    sforward \*STDIN, $pager_fh;
    close $pager_fh;
    $pager_fh->await;
    ni::procfh::kill_children 'TERM';
  } else {
    sio;
  }

  my $exit_status = 0;
  child_status_ok $_->await or $exit_status = 1 for @children;
  $exit_status;
};

Pagers and kill signals.
`less` resets a number of terminal settings, including character buffering and
no-echo. If we kill it directly with a signal, it will exit without restoring a
shell-friendly terminal state, requiring the user to run `reset` to fix it. So
in an interrupt context we try to give the pager a chance to exit gracefully by
closing its input stream and having the user use `q` or similar.

$SIG{TERM} = sub {
  close $pager_fh if $pager_fh;
  ni::procfh::kill_children 'TERM';
  exit 1;
};

$SIG{INT} = sub {
  if ($pager_fh) {
    close $pager_fh;
    $pager_fh->await;
  }
  ni::procfh::kill_children 'INT';
  exit 1;
};

c
BEGIN {
  defparseralias shell_lambda    => pn 1, prx '\[',  prep(prc '.*[^]]', 1), prx '\]';
  defparseralias shell_lambda_ws => pn 1, prc '\[$', prep(pnx '\]$',    1), prx '\]$';
}
BEGIN {
  defparseralias shell_command   => palt pmap(q{shell_quote @$_}, shell_lambda_ws),
                                         pmap(q{shell_quote @$_}, shell_lambda),
                                         prx '[^][]+';
}

defoperator cat  => q{my ($f) = @_; sio; scat $f};
defoperator echo => q{my ($x) = @_; sio; print "$x\n"};
defoperator sh   => q{my ($c) = @_; sh $c};

Note that we generate numbers internally rather than shelling out to `seq`
(which is ~20x faster than Perl for the purpose, incidentally). This is
deliberate: certain versions of `seq` generate floating-point numbers after a
point, which can cause unexpected results and loss of precision.

defoperator n => q{
  my ($l, $u) = @_;
  sio; for (my $i = $l; $u < 0 || $i < $u; ++$i) {print "$i\n"};
};

defshort '/n',   pmap q{n_op 1, defined $_ ? $_ + 1 : -1}, popt number;
defshort '/n0',  pmap q{n_op 0, $_}, number;
defshort '/id:', pmap q{echo_op $_}, prc '.*';

defshort '/e', pmap q{sh_op $_}, shell_command;

deflong '/fs', pmap q{cat_op $_}, filename;

Stream mixing/forking.
Append, prepend, duplicate, divert.

defoperator append => q{
  my @xs = @_;
  sio;
  exec_ni @xs;
};

defoperator prepend => q{
  my @xs = @_;
  close(my $fh = siproc {exec_ni @xs});
  $fh->await;
  sio;
};

defoperator duplicate => q{
  my @xs = @_;
  my $fh = siproc {exec_ni @xs};
  stee \*STDIN, $fh, \*STDOUT;
  close $fh;
  $fh->await;
};

defoperator sink_null => q{1 while saferead \*STDIN, $_, 8192};
defoperator divert => q{
  my @xs = @_;
  my $fh = siproc {close STDOUT; exec_ni @xs, sink_null_op};
  stee \*STDIN, $fh, \*STDOUT;
  close $fh;
  $fh->await;
};

defshort '/+', pmap q{append_op    @$_}, _qfn;
defshort '/^', pmap q{prepend_op   @$_}, _qfn;
defshort '/%', pmap q{duplicate_op @$_}, _qfn;
defshort '/=', pmap q{divert_op    @$_}, _qfn;

Interleaving.
Append/prepend will block one of the two data sources until the other
completes. Sometimes, though, you want to stream both at once. Interleaving
makes that possible, and you can optionally specify the mixture ratio, which is
the number of interleaved rows per input row. (Negative numbers are interpreted
as reciprocals, so -2 means two stdin rows for every interleaved.)

defoperator interleave => q{
  my ($ratio, $lambda) = @_;
  my $fh = soproc {close STDIN; exec_ni @$lambda};

  if ($ratio) {
    $ratio = 1/-$ratio if $ratio < 0;
    my ($n1, $n2) = (0, 0);
    while (1) {
      ++$n1, defined($_ = <STDIN>) || goto done, print while $n1 <= $n2 * $ratio;
      ++$n2, defined($_ = <$fh>)   || goto done, print while $n1 >= $n2 * $ratio;
    }
  } else {
    my $rmask;
    my ($stdin_ok,  $ni_ok) = (1, 1);
    my ($stdin_buf, $ni_buf);
    while ($stdin_ok || $ni_ok) {
      vec($rmask, fileno STDIN, 1) = $stdin_ok;
      vec($rmask, fileno $fh,   1) = $ni_ok;
      my $n = select my $rout = $rmask, undef, undef, 0.01;
      if (vec $rout, fileno STDIN, 1) {
        $stdin_ok = !!saferead \*STDIN, $stdin_buf, 1048576, length $stdin_buf;
        my $i = 1 + rindex $stdin_buf, "\n";
        if ($i) {
          safewrite \*STDOUT, substr $stdin_buf, 0, $i;
          $stdin_buf = substr $stdin_buf, $i;
        }
      }
      if (vec $rout, fileno $fh, 1) {
        $ni_ok = !!saferead $fh, $ni_buf, 1048576, length $ni_buf;
        my $i = 1 + rindex $ni_buf, "\n";
        if ($i) {
          safewrite \*STDOUT, substr $ni_buf, 0, $i;
          $ni_buf = substr $ni_buf, $i;
        }
      }
    }
  }

  done:
  close $fh;
  $fh->await;
};

defshort '/.', pmap q{interleave_op @$_}, pseq popt number, _qfn;

Sinking.
We can sink data into a file just as easily as we can read from it. This is
done with the `>` operator, which is typically written as `\>`. The difference
between this and the shell's > operator is that \> outputs the filename; this
lets you invert the operation with the nullary \< operator.

use constant tmpdir => $ENV{TMPDIR} || "/tmp";
sub tempfile_name() {
  my $r = '/';
  $r = tmpdir . "/ni-$$-" . noise_str 8 while -e $r;
  $r;
}

defoperator file_read  => q{chomp, weval q{scat $_} while <STDIN>};
defoperator file_write => q{
  my ($file) = @_;
  $file = tempfile_name unless defined $file;
  sforward \*STDIN, swfile $file;
  print "$file\n";
};

defshort '/>', pmap q{file_write_op $_}, nefilename;
defshort '/<', pmap q{file_read_op},     pnone;

Resource stream encoding.
This makes it possible to serialize a directory structure into a single stream.
ni uses this format internally to store its k/v state.

defoperator encode_resource_stream => q{
  my @xs;
  while (<STDIN>) {
    chomp;
    my $s = rfc $_;
    my $line_count = @xs = split /\n/, "$s ";
    print "$line_count $_\n", $s, "\n";
  }
};

defshort '/>\'R', pmap q{encode_resource_stream_op}, pnone;

Compression and decoding.
Sometimes you want to emit compressed data, which you can do with the `Z`
operator. It defaults to gzip, but you can also specify xz, lzo, lz4, or bzip2
by adding a suffix. You can decode a stream in any of these formats using `ZD`
(though in most cases ni will automatically decode compressed formats).

our %compressors = qw/ g gzip  x xz  o lzop  4 lz4  b bzip2 /;

c
BEGIN {defparseralias compressor_name => prx '[gxo4b]'}
BEGIN {
  defparseralias compressor_spec =>
    pmap q{my ($c, $level) = @$_;
           $c = $ni::compressors{$c || 'g'};
           defined $level ? sh_op "$c -$level" : sh_op $c},
    pseq popt compressor_name, popt integer;
}

defoperator decode => q{sdecode};

defshort '/z',  compressor_spec;
defshort '/zn', pk sink_null_op();
defshort '/zd', pk decode_op();
